= Enterprise Clojure Training
:copyright: Timothy Pratley
:license: Eclipse Public License http://www.eclipse.org/legal/epl-v10.html
include::slide.attrs[]


[state=title]
== 2. Clojure Syntax

image:img/syntax.png[]

"If the syntax is good enough for the information, it should be good enough for the meta-information."
-- Erik Naggum


== !

[options="header"]
|===
|Java |Clojure
l|int i = 5;
l|(def i 5)
l|if (x == 1)
  return y;
else
  return z;
l|(if (= x 1)
  y
  z)
l|x * y * z;
l|(* x y z)
l|foo(x, y, z);
l|(foo x y z)
l|foo.bar(x);
l|(.bar foo x)
|===

[NOTE.speaker]
--
Syntax Summary:

* Lisp is an acronym for "list processing", everything is a list!
* Operation appears at the front of the list, followed by arguments

--


== Lists

Evaluated as function calls

[source,eval-clojure]
    (inc 1)
    ;=> 2


== Invoking functions

[source,eval-clojure]
    (+ 1 2)
    ;=> 3

Always in prefix form


== Empty list

[source,eval-clojure]
    ()
    ;=> ()


== Making lists

[source,eval-clojure]
    (cons 1 ())
    ;=> (1)

[source,eval-clojure]
    (cons 1 (cons 2 ()))
    ;=> (1 2)

[source,eval-clojure]
    (list 1 2 3)
    ;=> (1 2 3)


== Vectors

    [1 2 3 4]

Preferred over lists; easier to write

Order 1 count and lookup by index

[source,eval-clojure]
    (get [10 20 30 40 50] 3)
    ;=> 40

Similar to arrays, but can be added to


== Vector operations

[source,eval-clojure]
    (conj [1 2 3] 4)
    ;=> [1 2 3 4]

[source,eval-clojure]
    (pop [1 2 3 4])
    ;=> [1 2 3]

[source,eval-clojure]
    (assoc [1 2 3 4] 0 5)
    ;=> [5 2 3 4]

[source,eval-clojure]
    (vec (list 1 2 3 4))
    ;=> [1 2 3 4]

[source,eval-clojure]
    (cons 0 [1 2 3 4])
    ;=> (0 1 2 3 4)


== Equality by value

[source,eval-clojure]
    (def a [1 2 3 4])
    (= a a)
    ;=> true

[source,eval-clojure]
    (def b [1 2 3 4])
    (= a b)
    ;=> true

NOTE: `a` and `b` are different objects

[source,eval-clojure]
    (identical? a b)
    ;=> false


== Sequential equality

[source,eval-clojure]
    (= [1 2 3 4] (list 1 2 3 4))
    ;=> true

Different types, but considered equal


== Symbols

* Usually `lowercase-words-hyphenated`
* Begin with an alphabet character
* Can contain numbers and punctuation
* Identifiers


== Resolving symbols

[source,eval-clojure]
    inc


[source,eval-clojure]
    foo


[source,eval-clojure]
    (quote foo)


[source,eval-clojure]
    'foo

`quote` means don't resolve or evaluate


== Quote also works on lists

[source,eval-clojure]
    (quote (1 2))
    ;=> (1 2)

[source,eval-clojure]
    '(1 2)
    ;=> (1 2)

Without quote we have a problem:

[source,eval-clojure]
    (1 2)
    ;=> Exception: Long cannot be cast to IFn

WARNING: Tried to apply 1 as a function


== !

[options="header"]
|===
|Type|Value
l|Long
l|1
l|Double
l|3.14
l|BigInteger
l|1000000000000N
l|BigDecimal
l|1000000000000.1M
l|Exponents
l|1e3
l|Ratio
l|2/5
|===

[NOTE.speaker]
--
* Representing Numbers
* Automatically promoted on overflow only when using *' now
--


== Strings and characters

    "This is a string."

Characters written with a backslash

    \a \b \c
    \newline \tab \space


== Maps

    {"name" "Fate of the Furious"
     "sequence-number" 8
     "rotten-tomatoes" 0.66
     "imdb" 0.67}

* Order 1 lookup, "add", "delete" by key
* Tuned to be fast
* Replacement for structs/objects
* Versatile; used often in Clojure code

[NOTE.speaker]
--
* There are fancier "versions" of maps in Clojure (records) to handle cases where you want faster Java interop or a strongly-typed name
* Records still "behave like a map" (implement the map interface)
* Maps are used instead of named function/method arguments in other languages
* Maps are used instead of value classes (immutable classes to hold data, impt for concurrent programming) because they are immutable
* For people from other languages, same as: dictionary (Python), hash (Ruby), object (JS), associative array (Perl)
--


== Keywords

* `:my-keyword`
* Shorthand identifiers
* Begin with a colon
* *Often used as keys in hashmaps*
--
    {:name "Fate of the Furious"
     :sequence-number 8
     :rotten-tomatoes 0.66
     :imdb 0.67}
--


== Map operations

[source,eval-clojure]
    (get {:a 1} :a)
    ;=> 1

[source,eval-clojure]
    (get {:a 1} :b 2)
    ;=> 2

[source,eval-clojure]
    (assoc {:a 1} :b 2)
    ;=> {:a 1, :b 2}

[source,eval-clojure]
    (dissoc {:a 1} :a)
    ;=> {}

[source,eval-clojure]
    (merge {:a 1} {:b 2})
    ;=> {:a 1, :b 2}


== Commas

Commas are optional and treated as whitespace

    (= {:a 1, :b 2, :c 3}
       {:a 1 :b 2 :c 3})

Prefer newlines

    {:a 1
     :b 2
     :c 3}


== Nesting datastructures

    {:name "Fate of the Furious"
     :sequence-number 8
     :ratings {:rotten-tomatoes 0.66
               :imdb 0.67}}

Ratings are a nested map

    {[1 2] {:name "diamond", :type :treasure}
     [3 4] {:name "dragon", :type :monster}}

A map with vector coordinate keys, and map values


== Nested updates

[source,eval-clojure]
    (update {:a 2} :a inc)
    ;=> {:a 3}

[source,eval-clojure]
    (update {:a [1 2 3]} :a conj 4)
    ;=> {:a [1 2 3 4]}

[source,eval-clojure]
    (assoc-in {:a {:b {:c 1}}} :d 2)
    ;=> {:a {:b {:c 1, :d 2}}}

[source,eval-clojure]
    (update-in {:a {:b {:c 1}}} [:a :b :c] inc)
    ;=> {:a {:b {:c 2}}}

[source,eval-clojure]
    (get-in {:a {:b {:c 1}}} [:a :b :c])


== Sets

    #{1 2 3}

Near constant time lookup

[source,eval-clojure]
    (contains? #{1 2 3} 3)
    ;=> true


== Set operations

[source,eval-clojure]
    (conj #{1 2 3} 4)
    ;=> #{1 2 3 4}

[source,eval-clojure]
    (disj #{1 2 3} 2)
    ;=> #{1 3}

`union`, `difference` and `intersection` are available in the `clojure.set` namespace


== Namespaces

`src/training/my_namespace.clj`

    (ns training.my-namespace
      (:require [clojure.set :as set])
      (:import (java.time Instant Duration)))
    (set/union #{1 2 3} #{3 4})
    => #{1 2 3 4}

* The name must match path and filename
* `my-namespace` -> `my_namespace.clj`
* `training.` -> `training/`

[NOTE.speaker]
--
* Must be the first form in the file
* Other forms exist, but prefer this one
--


== From the REPL

    (require '[clojure.set :as set])
    (import
    (use 'clojure.set)
    (require '[clojure.set :refer :all])

WARNING: Avoid `use` and `:refer :all`

TIP: `ns` works in the REPL!

    (ns my.namespace
       (:require [clojure.set :as set]))


== Programs

* Expressions which are evaluated to results
* If an expression needs to be compiled, it will be
* Can be loaded from files or evaluated dynamically
* Unit of compilation is a form
* Nominate an entry point namespace/function


== Namespaced keywords

    :my.namespace/rect

Shortcut:

[source,eval-clojure]
    ::rectangle
    ;=> :my.namespace/rectangle

`::` expands to the current namespace


== Defining functions

[source,eval-clojure]
    (defn square [x]
      (* x x))
    (square 2)
    ;=> 4

[source,eval-clojure]
    (defn square
    "Multiplies a number by itself"
    [x]
    (* x x))
    (square 2)
    ;=> 4


== Defining Vars

[source,eval-clojure]
    (def x 1)
    ;=> #'my.namespace/x

[source,eval-clojure]
    x
    ;=> 1

* Global mutable reference
* Use sparingly
* `#'` means `var`


== Accessing a var (not the value)

[source,eval-clojure]
    (var x)
    ;=> #'x

[source,eval-clojure]
    #'x
    ;=> #'x


== Defining Vars continued

* The symbol `x` resolves to a Var
* Vars are automatically dereferenced when evaluated
* Dereferrencing returns the value associated with the Var
* Avoid using vars like variables
* `defn` is actually `def` with a function value
* Can use `#'x` or `(var x)` to access the Var


== Let

[source,eval-clojure]
    (let [x 1]
      (inc x))
    ;=> 2

* Bind symbols to values in a scope
* Shadow existing bindings
* Prefer `let` over `def`


== Destructuring (binding forms)

[source,eval-clojure]
    (let [[x y] [1 2]]
      (+ x y))
    ;=> 3

* Literal data structure containing symbols
* Matches structure


== Why destructure?

    (defn normalize1 [v]
      (let [x (first v)
            y (second v)
            length (Math/sqrt (+ (* x x) (* y y)))]
        [(/ x length) (/ y length)]))

Avoid extracting substructure manually:

    (defn normalize2 [[x y]]
      (let [length (Math/sqrt (+ (* x x) (* y y)))]
        [(/ x length) (/ y length)]))


== Vector structures match any sequence

[source,eval-clojure]
    (let [[a b] (list 1 2)]
      b)
    ;=> 2


== Strings and Collections (list, vector, set, map) implement seq

[source,eval-clojure]
    (seq "abc")
    ;=> (\a \b \c)

[source,eval-clojure]
    (seq {:a 1, :b 2, :c 3})
    ;=> ([:a 1] [:b 2] [:c 3])

[source,eval-clojure]
    (seq? 8)
    ;=> false

[source,eval-clojure]
    (let [[a b] "abc"]
      b)
    ;=> \b


== Basic sequences

[source,eval-clojure]
    (drop 2 [0 0 0 0])
    ;=> (0 0)

[source,eval-clojure]
    (range 5)
    ;=> (0 1 2 3 4)

[source,eval-clojure]
    (take 2 "abcd")
    ;=> (\a \b)

* Many sequence oriented functions
* Never modify the original sequence
* Often lazy


== Lazy sequences

Lazy means that the next value in the sequence is only calculated when it is made use of

Stream abstraction; only the currently used item needs to be in memory

Useful for processing files that don't fit in memory


== For expressions

[source,eval-clojure]
    (for [i (range 10)]
      (* i i))
    ;=> (0 1 4 9 16 25 36 49 64 81)

[source,eval-clojure]
    (for [file ["a" "b" "c"]
          rank [1 2]]
      (str file rank))
    ;=> ("a1" "a2" "b1" "b2" "c1" "c2")

[source,eval-clojure]
    (for [i (range 10)
          :when (odd? i)
          :let [square (* i i)]]
      square)
    ;=> (1 9 25 49 81)


== Destructuring in a for expression

[source,eval-clojure]
    (let [m {:a 1, :b 2, :c 3}]
      (for [[k v] m]
        [v k]))
    ;=> ([1 :a] [2 :b] [3 :c])

TIP: Destructuring is available in any binding form


== Variadic functions using `&`

[source,eval-clojure]
    (defn sub [& vs]
      vs)
    (sub 1 2 3 4)
    ;=> (1 2 3 4)

* Variadic means variable number of arguments
* Arity means number of arguments
* We could have just passed a vector instead


== Apply

* Calls a function with a sequence of arguments

[source,eval-clojure]
    (apply + [1 2 3 4])
    ;=> 10

* Most mathematical functions are variadic:

[source,eval-clojure]
    (+ 1 2 3)
    ;=> 6


== Destructuring a map

[source,eval-clojure]
    (def x {:a 10
            :b 20})
    (let [{a :a, b :b} x]
      (+ a b))
    ;=> 30

[source,eval-clojure]
    (let [{:keys [a b]} x]
      (+ a b))
    ;=> 30


== Destructuring strings from a map

[source,eval-clojure]
    (def y {"a" 10
            "b" 20})
    (let [{a "a", b "b"} y]
      (+ a b))
    ;=> 30

[source,eval-clojure]
    (let [{:strs [a b]} y]
      (+ a b))
    ;=> 30


== Destructuring a sequence:

[source,eval-clojure]
    (def x (range 5))
    (first x)
    ;=> 0

[source,eval-clojure]
    (rest x)
    ;=> (1 2 3 4)

[source,eval-clojure]
    (let [[a & more] (range 5)]
      a)
    ;=> 0

[source,eval-clojure]
    (let [[a & more] (range 5)]
      more)
    ;=> (1 2 3 4)


== Nested destructuring

[source,eval-clojure]
    (def movie {:name "Fate of the Furious"
                :sequence-number 8
                :ratings {:rotten-tomatoes 0.66
                          :imdb 0.67}})
    (get-in movie [:ratings :imdb])
    ;=> 0.67

[source,eval-clojure]
    (let [{{:keys [imdb]} :ratings} movie]
      imdb)
    ;=> 0.67


== More destructuring

[source,eval-clojure]
    (defn f [{:keys [a b] :as x}]
      x)
    (f {})
    ;=> {}


== Destructuring with defaults

[source,eval-clojure]
    (defn f [{:keys [a b] :or {a "default"}}]
      a)
    (f {})
    ;=> "default"

[source,eval-clojure]
    (defn f [x]
      (let [defaults {:a "default"}]
            {:keys [a b]} (merge defaults x)]
        a))
    (f {})
    ;=> "default"


== Comments

Anything following a semicolon is a comment

    ; this is an inline comment
    ;; this is a function level comment

Less common is the comment form:

    (comment anything)


== Bug eye comments

Removal of next form `#_`

    #_(this form is removed)
    #_#_ (ignored-1) (ignored-2)

* Temporarily remove a form when debugging code
* Looks like a bug eyes emoji


== Regex

    #"pattern"

[source,eval-clojure]
    (re-seq #"\w+" "the quick brown fox")
    ;=> ("the" "quick" "brown" "fox")


== Exercises

See manual end of section 2


== Answers

Set up the new namespace called `training.syntax`

    (ns training.syntax)

Define a var called `message` bound to the string `"greetings"`

[source,eval-clojure]
    (def message "greetings")
    ;=> nil


== Answers

Print out the value of the var `message`

[source,eval-clojure]
    (prn message)
    ;;; "greetings"
    ;=> nil

NOTE: `prn` keeps the quotes around strings; `println` does not


== Answers

Create a `let` binding that binds the symbol `message` to `"well hello there"`, and prints out `message` inside the `let` block:

[source,eval-clojure]
    (let [message "well hello there"]
      (prn message))
    ;;; "well hello there"
    ;=> nil

Print out message again, outside of the `let` block:

[source,eval-clojure]
    (prn message)
    ;;; "greetings"
    ;=> nil

NOTE: `message` global var is still the original value


== Answers

Create a let binding that destructures a map and prints the greeting and tone:

[source,eval-clojure]
    (def m {:greeting "good morning", :tone "happy"})
    (let [{:keys [greeting tone]} m]
      (prn greeting tone))
    ;=> "good morning" "happy"


== Answers

Destructure a single map input and return a string combining greeting and tone:

[source,eval-clojure]
    (defn hi [{:keys [greeting tone]}]
      (str greeting " - " tone))
    (hi m)
    ;=> "good morning - happy"

